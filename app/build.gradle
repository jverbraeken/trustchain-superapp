apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.0.0'
    }
}

ktlint {
    version = "$ktlint_version"
    android = true
    outputToConsole = true
    ignoreFailures = false
}

// Generate a version code from git commit count
static def generateVersionCode() {
    int versionCode = "git rev-list HEAD --count".execute().text.trim().toInteger()
    print("Version code: $versionCode")
    return versionCode
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
//    ndkVersion "21.3.6528147"

    packagingOptions {
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
        pickFirst 'google/protobuf/**'
        pickFirst 'lib/arm64-v8a/**'
        pickFirst 'lib/x86/**'
        pickFirst 'lib/x86_64/**'
        pickFirst 'lib/armeabi-v7a/**'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'org/opencv/**'
        exclude 'org.opencv.**'
        exclude 'org.bytedeco.**'
        exclude 'LICENSE-EDL-1.0.txt'
        exclude 'LICENSE-EPL-1.0.txt'
    }

    applicationVariants.all { variant ->
        variant.getRuntimeConfiguration().exclude group: 'com.google.code.findbugs', module: 'jsr305'
//        variant.getRuntimeConfiguration().exclude group: 'net.java.dev.jna', module: 'jna'
//        variant.getRuntimeConfiguration().exclude group: 'com.google.guava', module: 'guava'
//        variant.getRuntimeConfiguration().exclude group: 'org.nd4j', module: 'guava'
    }

    lintOptions {
        abortOnError false
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings = true
        quiet true
        tasks.lint.enabled false
    }

    defaultConfig {
        applicationId "nl.tudelft.trustchain"
        minSdkVersion 29
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName "0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

//    externalNativeBuild {
//        cmake {
//            path "CMakeLists.txt"
//            cppFlags "-std=c++14 -fexceptions -frtti"
//            arguments "-DANDROID_ARM_NEON=TRUE",'-DANDROID_STL=c++_shared'
//        }
//    }

    signingConfigs {
        release {
            keyAlias "trustchain"
            keyPassword "ageoftrust"
            storeFile file("trustchain-superapp.jks")
            storePassword "ageoftrust"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // To inline the bytecode built with JVM target 1.8 into
    // bytecode that is being built with JVM target 1.6. (e.g. navArgs)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
    }

    buildFeatures {
        viewBinding = true
    }

    configurations {
        all*.exclude module: 'protobuf'
    }
}

repositories {
    maven {
        url = 'https://raw.githubusercontent.com/blockchain/api-v1-client-java/mvn-repo/'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':trustchain-voter')
    implementation project(':trustchain-explorer')
    implementation project(':fedml')
    implementation project(':trustchain-trader')
    implementation project(':trustchain-payloadgenerator')
    implementation project(':debug')
    implementation project(':distributedAI')
    implementation project(':currencyii')
    implementation project(':freedomOfComputing')
    implementation project(':musicdao')
    implementation project(':peerchat')

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.4.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    // Material
    implementation 'com.google.android.material:material:1.1.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.7.7'
    implementation 'com.github.tony19:logback-android:2.0.0'

    implementation 'com.mattskala:itemadapter:0.3'

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
        "-Xuse-experimental=kotlin.Experimental,kotlin.ExperimentalUnsignedTypes",
        "-Xopt-in=kotlin.RequiresOptIn"
    ]
}

